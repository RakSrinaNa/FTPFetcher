buildscript {
    ext {
        jsch_version = '0.1.55'
        slf4j_version = '2.0.0-alpha1'
        logback_version = '1.3.0-alpha5'
        raksrinana_utils_version = '14.0.14'
        commons_io_version = '2.6'
        progressbar_version = '0.8.1'
        picocli_version = '4.5.0'
        jackson_version = '2.11.0'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'com.novoda.build-properties' version '0.4.1'
    id 'idea'
    id 'java'
    id 'application'
    id 'io.freefair.lombok' version '5.1.1'
}

def secretsFile = new File(".", "secrets.properties")

if (!secretsFile.getParentFile().exists()) {
    secretsFile.mkdirs()
}

if (!secretsFile.exists()) {
    secretsFile.withWriterAppend { w ->
        w << "\n"
    }
}

buildProperties {
    secrets {
        using(file('secrets.properties')).or(System.getenv()).or([
                "GITHUB_USER" : "RakSrinaNa",
                "GITHUB_TOKEN": "N/A"
        ])
    }
}

group = 'fr.raksrinana'
description = 'FTPFetcher'
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_14

mainClassName = project.mainClass

dependencies {
    implementation("com.jcraft:jsch:${jsch_version}") {
        exclude module: 'commons-io'
        exclude module: 'commons-lang3'
    }

    implementation("org.slf4j:slf4j-api:${slf4j_version}")
    implementation("ch.qos.logback:logback-classic:${logback_version}") {
        exclude group: 'edu.washington.cs.types.checker', module: 'checker-framework'
        exclude group: 'javax.activation', module: 'activation'
    }
    implementation("fr.raksrinana:utils.base:${raksrinana_utils_version}")
    implementation("fr.raksrinana:utils.config:${raksrinana_utils_version}")

    implementation("com.fasterxml.jackson.core:jackson-core:${jackson_version}")
    implementation("com.fasterxml.jackson.core:jackson-annotations:${jackson_version}")
    implementation("com.fasterxml.jackson.core:jackson-databind:${jackson_version}")

    implementation("commons-io:commons-io:${commons_io_version}")
    implementation("me.tongfei:progressbar:${progressbar_version}") {
        exclude group: 'org.jline', module: 'jline'
    }
    implementation("info.picocli:picocli:${picocli_version}")
}

repositories {
    maven {
        url = 'https://maven.pkg.github.com/RakSrinaNa/JavaUtils/'
        credentials {
            username buildProperties.secrets['GITHUB_USER'].string
            password buildProperties.secrets['GITHUB_TOKEN'].string
        }
    }
    jcenter()
    mavenCentral()
}

processResources {
    expand project.properties
}

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

application {
    mainModule = project.moduleName
    mainClass = project.mainClass
}

java {
    modularity.inferModulePath.set(true)
}

shadowJar {
    archivesBaseName = project.name
    classifier = 'shaded'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.deprecation = true
}

tasks.withType(Wrapper) {
    gradleVersion = project.wrapper_version
}
